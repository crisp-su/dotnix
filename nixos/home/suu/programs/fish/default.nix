{
  dotnix,
  config,
  options,
  pkgs,
  lib,
  ...
}:

lib.mkMerge [
  {
    programs.fish = {
      enable = true;

      interactiveShellInit = /* bash */ ''
        # `nix` and `nix-shell` wrapper for fish shell
        ${pkgs.nix-your-shell}/bin/nix-your-shell fish | source
      '';

      shellAliases = {
        ip = "ip -c";
        grep = "grep --color=auto";
      };

      shellAbbrs = lib.mkMerge [
        {
          i = "fastfetch";
          e = config.home.sessionVariables.EDITOR or "nano";
          wlc = "wl-copy";
        }

        (lib.mkIf config.programs.bat.enable {
          b = "bat";
        })

        (lib.mkIf config.programs.git.enable {
          g = "git";
        })

        (lib.mkIf config.programs.eza.enable {
          l = "eza";
          la = "eza -a";
          ll = "eza -la";
          lt = "eza -Ta";
          llt = "eza -lTa";
        })
      ];
    };

    xdg.configFile =
      let
        subst = src: dotnix.lib.substituteDir { inherit src vars; };

        vars = {
          fastfetch = "${pkgs.fastfetch}/bin/fastfetch";
        };
      in
      {
        "fish/conf.d" = {
          source = subst ./conf.d;
          recursive = true;
        };
        "fish/functions" = {
          source = subst ./functions;
          recursive = true;
        };
      };
  }

  (lib.optionalAttrs (options.home ? persistence) {
    home.persistence."/persist" = {
      directories = [
        ".local/share/fish"
      ];
    };
  })

  (lib.optionalAttrs (options ? stylix) {
    stylix.targets.fish.enable = lib.mkDefault true;
  })

  (lib.mkIf
    (options ? catppuccin && lib.strings.hasPrefix "catppuccin-" config.settings.theme.colorscheme)
    (
      {
        catppuccin.fish.enable = true;
      }
      // lib.optionalAttrs (options ? stylix) {
        stylix.targets.fish.enable = false;
      }
    )
  )
]
